# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

# Based on: https://aur.archlinux.org/packages/carbonapi

pkgname=carbonapi
pkgver=0.15.2
pkgrel=1
pkgdesc='CarbonAPI - replacement for graphite-web written in Go'
arch=(i686 x86_64 armv6h armv7h aarch64)
url='http://github.com/go-graphite/carbonapi'
license=('MIT')
optdepends=('go-carbon')
makedepends=('go>=1.13.4')
backup=(
	"etc/${pkgname}.yaml"
)
source=(
	"${pkgname}-${pkgver}.tar.gz::http://github.com/go-graphite/carbonapi/archive/${pkgver}.tar.gz"
	"${pkgname}.yaml"
	"${pkgname}.service"
)
noextract=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('424fea603e377d16f72511744031ed10cdd5fb5e4a98a787c60fa2bafd814b48'
            '8a87c1016f1ff427e1e0fc8f58162e5e768554750f7265d03d99bc859da4bf0d'
            '22900a057e89b8b36d0af307299439b3c5aa3cdd9ec42efa14cbc2c79140f3e6')

prepare() {
	msg2 'Cache setup'
	local GOCACHE="$srcdir/cache/go"
	local GOTMP="$srcdir/tmp/go"
	mkdir -p "$GOCACHE"
	mkdir -p "$GOTMP"

	msg2 'GOPATH setup'
	local GOPATH="$srcdir/gopath"
	mkdir -p "$GOPATH/src/github.com/go-graphite"

	msg2 'Extracting sources'
	bsdtar -xf "$srcdir/$pkgname-$pkgver.tar.gz" -C "$GOPATH/src/github.com/go-graphite"
	mv "$GOPATH/src/github.com/go-graphite/$pkgname-$pkgver" "$GOPATH/src/github.com/go-graphite/$pkgname"
}

build() {
	local GOCACHE="$srcdir/tmp/cache"
	local GOTMP="$srcdir/tmp/go"
	local GOPATH="$srcdir/gopath"

	cd "$GOPATH/src/github.com/go-graphite/carbonapi"
	env \
		TMPDIR="$GOTMP" \
		GOCACHE="$GOCACHE" \
		GOPATH="$GOPATH" \
		GOTMPDIR="$GOTMP" \
		GOROOT_FINAL=/usr/lib/go \
		CGO_CPPFLAGS="${CPPFLAGS}" \
		CGO_CFLAGS="${CFLAGS}" \
		CGO_CXXFLAGS="${CXXFLAGS}" \
		go build \
			-buildmode=pie -mod=readonly -modcacherw -trimpath \
			-ldflags "-X main.BuildVersion=v$pkgver -s -w -extldflags \"${LDFLAGS}\""  \
			github.com/go-graphite/carbonapi/cmd/carbonapi
}

package() {
	local GOPATH="$srcdir/gopath"

	install -dm755 ${pkgdir}/usr/lib/systemd/system
	install -m644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service

	cd $GOPATH/src/github.com/go-graphite/${pkgname}

	install -dm755 ${pkgdir}/usr/bin
	install -Dm0755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"

	# installing minimal configuration file
	install -dm755 ${pkgdir}/etc
	install -m644 ${srcdir}/${pkgname}.yaml "${pkgdir}/etc/${pkgname}.yaml"

	# store original configuration file with comments
	install -dm755 ${pkgdir}/usr/share/${pkgname}
	install -m644 cmd/${pkgname}/${pkgname}.example.yaml "${pkgdir}/usr/share/${pkgname}/${pkgname}.yaml"
}
