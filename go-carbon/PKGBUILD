# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

# Based on: https://aur.archlinux.org/packages/go-carbon

pkgname=go-carbon
pkgver=0.16.2
pkgrel=1
pkgdesc='Golang implementation of Graphite/Carbon server with classic architecture: Agent -> Cache -> Persister'
arch=(i686 x86_64 armv6h armv7h aarch64)
url='http://github.com/go-graphite/go-carbon'
license=('MIT')
makedepends=('go>=1.13.4')
backup=(
	"etc/${pkgname}/${pkgname}.conf"
	"etc/${pkgname}/storage-aggregation.conf"
	"etc/${pkgname}/storage-schemas.conf"
)
source=(
	"${pkgname}-${pkgver}.tar.gz::http://github.com/go-graphite/go-carbon/archive/v${pkgver}.tar.gz"
	"${pkgname}.conf"
	'sysusers.conf'
	"${pkgname}.service"
)
noextract=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('05e464a7171a82da68bb8b1fa0b54cd9bd4f1613b061f83a97a07c3ba3f03242'
            '04c6c4a213e3184a4657eabc804937f881b6af21e2972f1bdcca36d0c6285e3c'
            '7f0e7ddaaa1da41d03b26882cba812d8879f38530d3bda197675039cef10c8b4'
            'a1dacf400628936b09ee86360f5452cf1e14e4a2391af3d7a0a192a7444dbbd2')

prepare() {
	msg2 'Cache setup'
	local GOCACHE="$srcdir/cache/go"
	local GOTMP="$srcdir/tmp/go"
	mkdir -p "$GOCACHE"
	mkdir -p "$GOTMP"

	msg2 'GOPATH setup'
	local GOPATH="$srcdir/gopath"
	mkdir -p "$GOPATH/src/github.com/go-graphite"

	msg2 'Extracting sources'
	bsdtar -xf "$srcdir/$pkgname-$pkgver.tar.gz" -C "$GOPATH/src/github.com/go-graphite"
	mv "$GOPATH/src/github.com/go-graphite/$pkgname-$pkgver" "$GOPATH/src/github.com/go-graphite/$pkgname"
}

build() {
	local GOCACHE="$srcdir/tmp/cache"
	local GOTMP="$srcdir/tmp/go"
	local GOPATH="$srcdir/gopath"

	cd "$GOPATH/src/github.com/go-graphite/$pkgname"
	env \
		TMPDIR="$GOTMP" \
		GOCACHE="$GOCACHE" \
		GOPATH="$GOPATH" \
		GOTMPDIR="$GOTMP" \
		GOROOT_FINAL=/usr/lib/go \
		CGO_CPPFLAGS="${CPPFLAGS}" \
		CGO_CFLAGS="${CFLAGS}" \
		CGO_CXXFLAGS="${CXXFLAGS}" \
		go build \
			-buildmode=pie -mod=readonly -modcacherw -trimpath \
			-ldflags "-X main.BuildVersion=v$pkgver -s -w  -extldflags \"${LDFLAGS}\""  \
			github.com/go-graphite/go-carbon
}

package() {
	local GOPATH="$srcdir/gopath"

	install -dm755 ${pkgdir}/usr/lib/sysusers.d
	install -m644 ${srcdir}/sysusers.conf ${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf

	install -dm755 ${pkgdir}/usr/lib/systemd/system
	install -m644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service

	cd $GOPATH/src/github.com/go-graphite/${pkgname}

	install -dm755 ${pkgdir}/usr/bin
	install -Dm0755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"

	install -dm755 ${pkgdir}/etc/${pkgname}
	install -m644 deploy/{storage-aggregation.conf,storage-schemas.conf} "${pkgdir}/etc/${pkgname}"
	# install minimal configuration file
	install -m644 ${srcdir}/${pkgname}.conf "${pkgdir}/etc/${pkgname}/${pkgname}.conf"

	# store original configuration file with comments
	install -dm755 ${pkgdir}/usr/share/${pkgname}
	install -m644 go-carbon.conf.example "${pkgdir}/usr/share/${pkgname}/${pkgname}.conf"
}
